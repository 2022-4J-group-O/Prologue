"""
会話パート: 最初に少女と出会うシーン
"""
label d01:
    # TODO: 右側から少女が入ってくる演出を入れたい
    scene bg room 
    with fade

    show girl
    # cpsを変更
    $ cps(10)
    "......"

    $ cps(30)
    g "この場所が読み込まれるなんて、ずいぶん久しぶりだね"

    g "君は......どうやら私の知り合いではないみたい"

    show girl smile

    g "君が誰なのかはよくわからないけど、人と話せてうれしい"

    "......"

    show girl look away

    g "......ところでさ、どうして君がこのプログラムを起動したのか私にはわからないんだけど......"

    show girl
    
    g "ゲームを遊びに来た......ってことでいいんだよね？"

    "......"

    show girl smile

    g "なんにせよ、私との遊びに付き合ってほしいんだけど、どうかな？"

    show girl look away

    g "とにかく暇なんだよ......私ここにずっと一人だからさ......"

    "......"

    show girl

    g "このゲームは、脱出ゲームだよ"

    g "特に理由もなく狭い部屋に閉じ込められて、どういう仕組みなのかよくわからない珍妙な機械にパスワードを入力して......"

    g "とにかくそこから出ることだけを目指して進んでいく、あの、脱出ゲーム"

    "......"

    g "きっと、こんな時間からこんなゲーム起動した君の、暇つぶしくらいにはなるよ"

    show girl smile

    g "とにかく、私の暇を紛らわせてほしい"

    show girl
    
    g "きっと、きみを退屈させないって約束するからさ、どう？"

    $ flg_d01_no = False  # 一回以上d01_noにジャンプしたか

label d01_q:
    menu:
        "遊びに付き合う":
            jump d01_yes
        "断る":
            jump d01_no

label d01_yes:
    show girl
    
    g "ありがとう！"

    g "それじゃさっそく、脱出ゲームスタートだよ"

    g "......まずは、この部屋から脱出しないとね"

    g "この部屋の探索から始めてみたら？"

    hide girl

    jump e02 

label d01_no:
    if not flg_d01_no:
        $ flg_d01_no = True
        
        show girl

        "......"
        
        show girl smile

        g "だよねー、まずは断って、キャラのリアクション見てみないといけないよね"

        g "了承してしまったら、断った時にどういう反応するのか見れないもんね"

        g "拒否権のない質問であることは分かったうえで、拒否せざるを得ない......"

        g "ゲームプレイヤーっていうのは、そういう生き物だもんね"

        g "というわけで、断ってもどうせまたループさせるわけなんだけど、改めてもう一回聞くね？"

        g "儀式みたいなもんだからさ"

        g "ゲームに付き合ってくれるよね？"
    
    else:
        show girl

        "......"

        show girl smile

        g "なかなか慎重派なプレイヤーさんだね"

        g "これ以上違うイベントは起こさないよ。安心して"

    jump d01_q